#version 460 core

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(binding = 0) uniform sampler2D u_InputTexture;
layout(binding = 0, rgba8) writeonly uniform image2D u_Output;

layout(location = 0) uniform int u_Width;
layout(location = 1) uniform int u_Height;

vec3 aces(vec3 x)
{
    const float a = 2.51;
    const float b = 0.03;
    const float c = 2.43;
    const float d = 0.59;
    const float e = 0.14;

    return clamp((x * (a * x + b)) / (x * (c * x + d) + e), 0.0, 1.0);
}

void main()
{
    ivec2 coords = ivec2(gl_GlobalInvocationID.xy);
    if (coords.x >= u_Width || coords.y >= u_Height) {
        return;
    }

    vec4 source = texelFetch(u_InputTexture, coords, 0);

    vec3 color = source.rgb;
    if (source.a > 0.0f) {
        color = color / source.a;
    }

    color = aces(color);
    color = pow(color, vec3(1.0 / 2.2));

    imageStore(u_Output, coords, vec4(color, 1.0));
}
